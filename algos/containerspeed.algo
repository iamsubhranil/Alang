Set front = Null

Container Node(x)
    Set value = x
    Set next = Null
EndContainer

Routine insertAtEnd(x)
    If(front == Null)
        Set front = x
    Else
        Set bak = front
        While(bak.next != Null)
            Set bak = bak.next
        EndWhile
        Set bak.next = x
    EndIf
    Print "\nInsertion complete! "
EndRoutine

Routine insertAtFront(x)
    Set x.next = front, front = x
EndRoutine

Routine deleteFromFront()
    If(front == Null)
        Print "\nList is empty! Unable to delete!"
    Else
        Set front = front.next
        Print "\nDeletion succeeded!"
    EndIf
EndRoutine

Routine deleteValue(x)
    Set bak = front
    If(front == Null)
        Print "\nList is empty! Unable to delete!"
        Return
    Else If(front.value == x)
        Call deleteFromFront()
        Return
    EndIf
    While(bak.next != Null)
        If(bak.next.value == x)
            Set bak.next = bak.next.next
            Print "\nDeletion succeeded!"
            Return
        EndIf
        Set bak = bak.next
    EndWhile
    Print "\nDeletion failed!"
EndRoutine

Routine deleteFromEnd()
    If(front == Null)
        Print "\nList is empty! Unable to delete!"
    Else If(front.next == Null)
        Call deleteFromFront()
    Else
        Set bak = front
        While(bak.next.next != Null)
            Set bak = bak.next
        EndWhile
        Set bak.next = bak.next.next
        Print "\nDeletion succeeded!"
    EndIf
EndRoutine

Routine insertAtPos(x, pos)
    If(pos == 1)
        Call insertAtFront(x)
        Return
    EndIf
    Set i = 1, bak = front
    While(i < (pos - 1) And bak != Null)
        Set bak = bak.next
        Set i = i+1
    EndWhile
    If(bak == Null)
        Print "\nInvalid position specified : ", pos
    Else
        Set x.next = bak.next, bak.next = x
        Print "\nInsertion complete! "
    EndIf
EndRoutine

Routine PrintList(x)
    If(x == Null)
        Print "\nEmpty list!"
        Return
    EndIf
    Set bak = x, i = 1
    While(bak != Null)
        Print "\nNode : ",i,"\tValue : ",bak.value
        Set bak = bak.next, i = i+1
    EndWhile
EndRoutine

Routine NewNode()
    Print "\nEnter the value of the node : "
    Input x:Int
    Return Node(x)
EndRoutine
Routine Main()
    Set i = 0
    While(i < 10000)
        Call insertAtFront(Node(i))
        Set i = i + 1
    EndWhile
EndRoutine
